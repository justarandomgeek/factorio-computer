program "TEST PROGRAM"
rIndex.signal-red = 50
call CLRDISP
// Print helloworld
rNixie = [helloworld]

call COLORDEMO
call CLRDISP
call MATHDEMO
call KEYDEMO
{}

helloworld: "HELLO WORLD"

function CLRDISP:
  // Chear Display
  rNixie.signal-grey = 1
  rNixie.signal-grey = 1
  rNixie.signal-grey = 1
  rNixie.signal-grey = 1
  rNixie.signal-grey = 1
end

function PRINTBLOCK:
  pop 4 rNixie
  pop 4 rNixie
  pop 4 rNixie
  pop 4 rNixie
  pop 4 rNixie
end

function READKEY:
  r4=rKeyboard
  while r4.signal-each == 0
    r4=rKeyboard
  end
  rKeyboard.signal-grey=1
  return
end

function READLINE:
  push 1 r3
  r3.signal-grey = 1
  call READKEY
  while r4.signal-grey == 0
    r5.signal-grey = r4.signal-red+r4.signal-green
    r5.signal-grey+=r4.signal-blue+r4.signal-cyan
    r5.signal-grey+=r4.signal-pink+r4.signal-yellow
    rNixie=r4*r3.signal-grey
    if r5.signal-grey == 0
      r3.signal-grey+=r3.signal-grey
    end
    call READKEY
  end
  pop 1 r3
end

function KEYDEMO:
  while rNull.Null == rNull.Null
    call READLINE
    rNixie.signal-grey=1
  end
end

function COLORDEMO:
  // Print a color demo
  rIndex.signal-green += colors
  rNixie.signal-grey = 1
  pop 2 rNixie
  rNixie.signal-grey = 1
  pop 2 rNixie
  rNixie.signal-grey = 1
  pop 2 rNixie
  return
  colors:   { .signal-red=1 .signal-green=2 .signal-blue=4
              .signal-cyan=8 .signal-pink=16 .signal-yellow=32
            }&"COLORS"
            {.signal-red=7 .signal-green=496 .signal-blue=61440
            }&"RED GREEN   BLUE"
            {.signal-cyan=15 .signal-pink=480 .signal-yellow=258048
            }&"CYAN PINK   YELLOW"
end


function MATHDEMO:
  // nn FACTORIAL
  // nn SQUARE
  // ii NUMBER
  r1.signal-white = 1
  r1+=[number]
  r3.signal-white = 1

  while r1.signal-white < 11 //TODO: convert this to do/while?
    [500]=r1

    r2.signal-white = r1.signal-white * r1.signal-white
    r2+=[square]
    [501]=r2

    r3.signal-white = r3.signal-white * r1.signal-white
    r3+=[factorialstr]
    [502]=r3

    r1.signal-white += 1
  end
  return

  number:        "NUMBER"
  square:        "SQUARE"
  factorialstr:  "FACTORIAL"
end


// External devices
// nixies - [500] is rNixie, succesive lines above
//nixie:500@extern
//nixie1:extern
//nixie2:extern
//nixie3:extern
//nixie4:extern
// */
